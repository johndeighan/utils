# cleanup.civet

import {expandGlobSync} from '@std/fs/expand-glob'
import pathLib from 'node:path'

# ---------------------------------------------------------------------------

relpath := (path: string) =>

	return pathLib.relative('', path).replaceAll('\\', '/')

# ---------------------------------------------------------------------------

rootDir: string? := Deno.env.get('PROJECT_ROOT_DIR')
if (rootDir == undefined)
	console.log "Please set env var PROJECT_ROOT_DIR"
	Deno.exit()

listOnly := (Deno.args[0] != undefined)

lFiles: string[] := []  # NOTE: a const, but can still push strings
for pattern of [
		'**/*.js'
		'compile.config.ts'
		'src/**/*.ts'
		'src/**/*.temp.*'
		'test/**/*.ts'
		'test/**/*.temp.*'
		'logs/logs.txt'
		]
	hGlobOptions := {
		exclude: [
			'node_modules/**'
			'.git/**'
			'test/temp.ts'
			]
		includeDirs: false
		}

	for {path} of expandGlobSync(pattern, hGlobOptions)
		lFiles.push path

if (lFiles.length == 0)
	console.log "No files to remove"
else
	for path of lFiles
		if listOnly
			console.log "WOULD REMOVE: #{path}"
		else
			console.log "REMOVE: #{relpath(path)}"
			Deno.removeSync path
	console.log "#{lFiles.length} files removed"
